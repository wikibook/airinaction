<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" 
    layout="absolute" creationComplete="creationCompleteHandler();">                      
    <mx:Script>
        <![CDATA[
            import mx.controls.Image;
            
            private function creationCompleteHandler():void {
                addEventListener(NativeDragEvent.NATIVE_DRAG_ENTER, nativeDragEnterHandler);                                                               
                addEventListener(NativeDragEvent.NATIVE_DRAG_DROP, nativeDragDropHandler);                                                                
                var directory:File = File.applicationStorageDirectory.resolvePath("images");                                
                if(!directory.exists) {                                                    
                    directory.createDirectory();                                           
                }                                                                          
                var images:Array = directory.getDirectoryListing();                        
                for(var i:Number = 0; i < images.length; i++) {                            
                    displayImage(images[i]);                                               
                }                                                                          
            }
            
            private function displayImage(file:File):void {
                var image:Image = new Image();
                image.source = file.nativePath;
                image.scaleContent = true;
                image.maintainAspectRatio = true;
                image.width = 100;
                tile.addChild(image);
            }
            
            private function nativeDragEnterHandler(event:NativeDragEvent):void {
                if(event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT)) {         
                    NativeDragManager.acceptDragDrop(this);                                
                }
            }
            
            private function nativeDragDropHandler(event:NativeDragEvent):void {
                var files:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;                                           
                var file:File;
                var newLocation:File;
                for(var i:Number = 0; i < files.length; i++) {
                    file = files[i] as File;
                    newLocation = File.applicationStorageDirectory.resolvePath("images/image" + (new Date()).getTime() + i + ".jpg");               
                    file.moveTo(newLocation);                                             
                    displayImage(newLocation);                                            
                }
            }
            
        ]]>
    </mx:Script>
    
    <mx:Tile id="tile" width="100%" height="100%" />
</mx:WindowedApplication>
