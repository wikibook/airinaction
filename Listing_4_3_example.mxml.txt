<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" 
    creationComplete="creationCompleteHandler();">
    <mx:Script>
        <![CDATA[
            
            private var _focused:TextArea;
            private var _clipboard:Clipboard;
            
            private function creationCompleteHandler():void {
                _clipboard = Clipboard.generalClipboard;
                var menu:NativeMenu = new NativeMenu();
                var editMenu:NativeMenu = new NativeMenu();
                var copyItem:NativeMenuItem = new NativeMenuItem("copy");
                copyItem.addEventListener(Event.SELECT, copyText);
                var pasteItem:NativeMenuItem = new NativeMenuItem("paste");
                pasteItem.addEventListener(Event.SELECT, pasteText);
                editMenu.addItem(copyItem);
                editMenu.addItem(pasteItem);
                menu.addSubmenu(editMenu, "edit");
                if(NativeApplication.supportsMenu) {
                    nativeApplication.menu = menu;
                }
                else if(NativeWindow.supportsMenu) {
                    nativeWindow.menu = menu;
                }
            }
            
            private function copyText(event:Event):void {
                var text:String = _focused.text.substring(_focused.selectionBeginIndex, _focused.selectionEndIndex);
                _clipboard.clear();
                _clipboard.setData(ClipboardFormats.TEXT_FORMAT, text);
            }
            
            private function pasteText(event:Event):void {
                var text:String = _clipboard.getData(ClipboardFormats.TEXT_FORMAT) as String;                
                var currentText1:String = _focused.text.substr(0, _focused.selectionBeginIndex);
                var currentText2:String = _focused.text.substr(_focused.selectionEndIndex);
                _focused.text = currentText1 + text + currentText2;
            }
            
        ]]>
    </mx:Script>
    <mx:HBox width="100%" height="100%">
        <mx:TextArea id="textAreaA" width="50%" height="100%" 
            focusIn="_focused = textAreaA;">
            <mx:text>Lorem ipsum dolor sit amet, consectetuer adipiscing elit.</mx:text>
        </mx:TextArea>
        <mx:TextArea id="textAreaB" width="50%" height="100%" 
            focusIn="_focused = textAreaB;" />
    </mx:HBox>
</mx:WindowedApplication>
