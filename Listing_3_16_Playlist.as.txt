package com.manning.playlistmaker.data {
    import flash.events.Event;
    import flash.events.EventDispatcher;
    import flash.filesystem.File;

    [RemoteClass(alias="com.manning.playlistmaker.data.Playlist")]                     
    public class Playlist extends EventDispatcher {
        
        private var _list:Array;                                                       
        private var _name:String;                                                      
        
        [Bindable(event="listChanged")]                                                
        public function get list():Array {
            return _list;
        }
        
        public function set list(value:Array):void {
            _list = value;
        }
        
        [Bindable(event="nameChanged")]
        public function set name(value:String):void {
            _name = value;
        }
        
        public function get name():String {
            return _name;
        }
        
        public function Playlist() {
            if(_list == null) {                                                         
                _list = new Array();
            }
        }
        
        public function addTrack(value:File):void {                                     
            _list.push(value.name);                                                     
            dispatchEvent(new Event("listChanged"));                                    
        }
        
        public function addTracks(value:Array):void {                                   
            _list = _list.concat(value);
            dispatchEvent(new Event("listChanged"));
        }
        
        public function removeTrack(value:File):void {                                  
            for(var i:Number = 0; i < _list.length; i++) {
                if(_list[i].path == value.nativePath) {
                    _list.splice(i, 1);
                    break;
                }
            }
            dispatchEvent(new Event("listChanged"));
        }
        
    }
}
